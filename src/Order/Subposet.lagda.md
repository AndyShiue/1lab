<!--
```agda
open import 1Lab.Prelude

open import Cat.Displayed.Total

open import Order.Base

import Order.Reasoning as Poset
```
-->

```agda
module Order.Subposet where
```

<!--
```agda
open Total-hom
```
-->

# Sub-posets {defines="subposet"}

A **subposet** of a [[poset]] $X$ is the poset generated by some
[[predicate|proposition]] on the underlying set of $X$. In more abstract
terms, it is the order-theoretic analog of a [[full subcategory]].

```agda
module _ {o ℓ} (X : Poset o ℓ) where
  open Poset-on
  private
    module X = Poset X

  Subposet'
    : ∀ {ℓ'} (P : X.Ob → Type ℓ') → (∀ x → is-prop (P x)) → Poset (o ⊔ ℓ') ℓ
  Subposet' P pprop .fst .∣_∣ = Σ[ x ∈ X.Ob ] (P x)
  Subposet' P pprop .fst .is-tr =
    Σ-is-hlevel 2 (X .fst .is-tr) λ x → is-prop→is-set (pprop x)

  Subposet' P _ .snd ._≤_ px py = (px .fst) X.≤ (py .fst)
  Subposet' P _ .snd .has-is-poset .is-partial-order.≤-thin = X.≤-thin
  Subposet' P _ .snd .has-is-poset .is-partial-order.≤-refl = X.≤-refl
  Subposet' P _ .snd .has-is-poset .is-partial-order.≤-trans = X.≤-trans

  Subposet' P pprop .snd .has-is-poset .is-partial-order.≤-antisym p q =
    Σ-prop-path (λ x → pprop x) (X.≤-antisym p q)

  Subposet : (P : X.Ob → Ω) → Poset o ℓ
  Subposet P = Subposet' (λ x → x ∈ P) (λ x → P x .is-tr)

module _ {o ℓ} {X : Poset o ℓ} {P : Poset.Ob X → Ω} where
  Forget-subposet : Posets.Hom (Subposet X P) X
  Forget-subposet .Total-hom.hom = fst
  Forget-subposet .Total-hom.preserves _ _ p = p
```

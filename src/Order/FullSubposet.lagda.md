<!--
```agda
open import 1Lab.Prelude

open import Cat.Displayed.Total

open import Order.Base

import Order.Reasoning as Poset
```
-->

```agda
module Order.FullSubposet where
```

<!--
```agda
open Total-hom
```
-->

# Full subposets {defines="full-subposet"}

A **full subposet** of a [[poset]] $X$ is the poset generated by some
[predicate] on the underlying set of $X$. In more abstract terms, it
is the order-theoretic analog of a [[full subcategory]].

```agda
module _ {o ℓ} (X : Poset o ℓ) where
  open Poset-on
  private
    module X = Poset X
  
  Full-subposet
    : ∀ (P : X.Ob → Ω) → Poset o ℓ
  Full-subposet P .fst .∣_∣ = Σ[ x ∈ X.Ob ] (x ∈ P)
  Full-subposet P .fst .is-tr =
    Σ-is-hlevel 2 (X .fst .is-tr) λ x → is-prop→is-set (P x .is-tr)
  Full-subposet P .snd ._≤_ px py = (px .fst) X.≤ (py .fst)
  Full-subposet P .snd .has-is-poset .is-partial-order.≤-thin = X.≤-thin
  Full-subposet P .snd .has-is-poset .is-partial-order.≤-refl = X.≤-refl
  Full-subposet P .snd .has-is-poset .is-partial-order.≤-trans = X.≤-trans
  Full-subposet P .snd .has-is-poset .is-partial-order.≤-antisym p q =
    Σ-prop-path (λ x → P x .is-tr) (X.≤-antisym p q)

module _ {o ℓ}
  {X : Poset o ℓ}
  {P : Poset.Ob X → Ω}
  where

  Forget-full-subposet : Posets.Hom (Full-subposet X P) X
  Forget-full-subposet .Total-hom.hom = fst
  Forget-full-subposet .Total-hom.preserves _ _ p = p
```
